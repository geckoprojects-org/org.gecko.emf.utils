// Available to customize the build
plugins {
  id "org.sonarqube" version "3.3"
  id 'jacoco'
  id 'java'
}

sonarqube {
  properties {
    property "sonar.login", "sqp_775777110540c0509ea87c00a0e0fcf065a4bf70"
    property "sonar.projectName", "Gecko EMF Utilities"
    property "sonar.projectKey", "geckoEMFUtil"
    property "sonar.host.url", "http://localhost:9000"
    property "sonar.coverage.jacoco.xmlReportPaths", "${rootDir}/build/reports/jacoco/codeCoverageReport/codeCoverageReport.xml"
    property "sonar.junit.reportPaths", "${project.buildDir}/reports/jacoco.xml"
    property "sonar.exclusions", "org.gecko.emf.collections/**/*.java, org.gecko.emf.pushstreams/**/*.java, org.gecko.emf.util.model/**/*.java, **/*Test.java"
  }
}


repositories {
    mavenCentral()
}

tasks.named('wrapper') {
  jarFile = rootProject.file('.gradle-wrapper/gradle-wrapper.jar')
}

// Disable bnd plugin built-in task 'testOSGi' and register custom task for testing
gradle.startParameter.excludedTaskNames += "testOSGi"

tasks.register('itest'){
  subprojects.each { sp ->
    sp.tasks.each { t -> 
      if(t.name ==~ "resolve\\.(test|export)(.*)?"){
        dependsOn t
      }
    }
  }
  subprojects.each { sp ->
    sp.tasks.each { t -> 
      if(t.name ==~ "testrun\\.test(.*)?"){
        dependsOn t
      }
    }
  }
  subprojects.each { sp ->
    sp.tasks.each { t -> 
      if(t.name ==~ "export\\.export(.*)?"){
        dependsOn t
      }
    }
  }
}

subprojects {
	tasks.withType(Test) {
		useJUnitPlatform()
		testLogging {
			setExceptionFormat("full")
			info {
				events("STANDARD_OUT", "STANDARD_ERROR", "STARTED", "FAILED", "PASSED", "SKIPPED")
			}
		}
	}
}


task codeCoverageReport(type: JacocoReport) {
    dependsOn itest
    dependsOn test
    // Gather execution data from all subprojects
    executionData fileTree(project.rootDir.absolutePath).include("**/**/jacoco.exec")

    // Add all relevant sourcesets from the subprojects
    subprojects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        xml.enabled true
        html.enabled true
        csv.enabled false
    }
}

tasks.named("sonarqube") {
    dependsOn(codeCoverageReport)
}

